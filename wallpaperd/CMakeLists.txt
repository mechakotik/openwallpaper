cmake_minimum_required(VERSION 4.0)
project(wallpaperd)

include(GNUInstallDirs)
find_package(PkgConfig)

option(WD_WLROOTS "Enable wlroots output support" ON)
option(WD_AUDIO_VISUALIZER "Enable audio visualization support" ON)

option(WD_SDL_SHARED "Dynamically link to system SDL" OFF)
option(WD_SDL_IMAGE_SHARED "Dynamically link to system SDL_image" OFF)
option(WD_SDL_SHADERCROSS_SHARED "Dynamically link to system SDL_shadercross" OFF)
option(WD_LIBZIP_SHARED "Dynamically link to system libzip" OFF)
option(WD_FFTW_SHARED "Dynamically link to system FFTW" OFF)

option(WD_SANITIZE "Build with sanitizers" OFF)

set(BUILD_SHARED_LIBS OFF)

if(WD_SANITIZE)
    add_compile_options(-fsanitize=address,leak,undefined -fno-sanitize=alignment)
    add_link_options(-fsanitize=address,leak,undefined -fno-sanitize=alignment)
endif()

file(GLOB_RECURSE WD_SOURCES src/*.c)
add_executable(wallpaperd ${WD_SOURCES})
target_include_directories(wallpaperd PRIVATE include)

if(WD_SDL_SHARED)
    find_package(SDL3 REQUIRED CONFIG REQUIRED COMPONENTS SDL3-shared)
    target_link_libraries(wallpaperd PRIVATE SDL3::SDL3-shared)
else()
    set(SDL_STATIC ON)
    set(SDL_DISABLE_INSTALL ON)
    add_subdirectory(external/SDL EXCLUDE_FROM_ALL)
    target_link_libraries(wallpaperd PRIVATE SDL3::SDL3-static)
    target_include_directories(wallpaperd PRIVATE external/SDL/include)
endif()

if(WD_SDL_IMAGE_SHARED)
    find_package(SDL3_image REQUIRED CONFIG REQUIRED COMPONENTS SDL3_image-shared)
    target_link_libraries(wallpaperd PRIVATE SDL3_image::SDL3_image-shared)
else()
    set(SDLIMAGE_VENDORED ON)
    set(SDLIMAGE_DEPS_SHARED OFF)
    set(SDLIMAGE_INSTALL OFF)
    set(SDLIMAGE_PNG ON)
    set(SDLIMAGE_WEBP ON)
    set(SDLIMAGE_AVIF OFF)
    set(SDLIMAGE_BMP OFF)
    set(SDLIMAGE_GIF OFF)
    set(SDLIMAGE_JPG OFF)
    set(SDLIMAGE_JXL OFF)
    set(SDLIMAGE_LBM OFF)
    set(SDLIMAGE_PCX OFF)
    set(SDLIMAGE_PNM OFF)
    set(SDLIMAGE_QOI OFF)
    set(SDLIMAGE_SVG OFF)
    set(SDLIMAGE_TGA OFF)
    set(SDLIMAGE_TIF OFF)
    set(SDLIMAGE_XCF OFF)
    set(SDLIMAGE_XPM OFF)
    set(SDLIMAGE_XV OFF)
    add_subdirectory(external/SDL_image EXCLUDE_FROM_ALL)
    target_link_libraries(wallpaperd PRIVATE SDL3_image::SDL3_image-static)
    target_include_directories(wallpaperd PRIVATE external/SDL_image/include)
endif()

if(WD_SDL_SHADERCROSS_SHARED)
    find_package(SDL3_shadercross REQUIRED CONFIG REQUIRED COMPONENTS SDL3_shadercross-shared)
    target_link_libraries(wallpaperd PRIVATE SDL3_shadercross::SDL3_shadercross-shared)
else()
    set(SDLSHADERCROSS_VENDORED ON)
    set(SDLSHADERCROSS_STATIC ON)
    set(SDLSHADERCROSS_DXC OFF)
    set(SDLSHADERCROSS_SPIRVCROSS_SHARED OFF)
    add_subdirectory(external/SDL_shadercross EXCLUDE_FROM_ALL)
    target_link_libraries(wallpaperd PRIVATE SDL3_shadercross::SDL3_shadercross-static)
    target_include_directories(wallpaperd PRIVATE external/SDL_shadercross/include)
endif()

if(WD_LIBZIP_SHARED)
    find_package(PkgConfig REQUIRED)
    pkg_check_modules(libzip REQUIRED IMPORTED_TARGET libzip)
    target_link_libraries(wallpaperd PRIVATE PkgConfig::libzip)
else()
    if(NOT TARGET ZLIB::ZLIB)
        set(ZLIB_BUILD_SHARED OFF)
        add_subdirectory(external/zlib EXCLUDE_FROM_ALL)
        # hack to make find_package(ZLIB) use static zlib
        add_library(ZLIB::ZLIB ALIAS zlibstatic)
    endif()

    set(BUILD_DOC OFF CACHE BOOL "" FORCE)
    set(BUILD_EXAMPLES OFF CACHE BOOL "" FORCE)
    set(BUILD_TOOLS OFF CACHE BOOL "" FORCE)
    set(ENABLE_BZIP2 OFF CACHE BOOL "" FORCE)
    set(ENABLE_LZMA OFF CACHE BOOL "" FORCE)
    set(ENABLE_ZSTD OFF CACHE BOOL "" FORCE)
    set(LIBZIP_DO_INSTALL OFF CACHE BOOL "" FORCE)
    add_subdirectory(external/libzip EXCLUDE_FROM_ALL)
    target_link_libraries(wallpaperd PRIVATE zip zlibstatic)
endif()

set(WAMR_BUILD_PLATFORM "linux")
set(WAMR_BUILD_TARGET "X86_64")
set(WAMR_BUILD_INTERP 1)
set(WAMR_BUILD_FAST_INTERP 1)
set(WAMR_BUILD_AOT 1)
set(WAMR_BUILD_LIBC_BUILTIN 1)
set(WAMR_BUILD_LIBC_WASI 1)
set(WAMR_BUILD_REF_TYPES 1)
set(WAMR_ROOT_DIR external/wasm-micro-runtime)

include(${WAMR_ROOT_DIR}/build-scripts/runtime_lib.cmake)
include(${SHARED_DIR}/utils/uncommon/shared_uncommon.cmake)
add_library(vmlib ${WAMR_RUNTIME_LIB_SOURCE})
target_sources(wallpaperd PRIVATE ${UNCOMMON_SHARED_SOURCE})
target_link_libraries(wallpaperd PRIVATE vmlib)

if(WD_WLROOTS)
    function(add_protocol PROTOCOL_NAME)
        set(PROTOCOL_XML "${CMAKE_CURRENT_SOURCE_DIR}/wayland/${PROTOCOL_NAME}.xml")
        set(PROTOCOL_C "${CMAKE_CURRENT_BINARY_DIR}/${PROTOCOL_NAME}.c")
        set(PROTOCOL_H "${CMAKE_CURRENT_BINARY_DIR}/${PROTOCOL_NAME}.h")

        add_custom_command(
            OUTPUT ${PROTOCOL_C} ${PROTOCOL_H}
            COMMAND ${WAYLAND_SCANNER} private-code ${PROTOCOL_XML} ${PROTOCOL_C}
            COMMAND ${WAYLAND_SCANNER} client-header ${PROTOCOL_XML} ${PROTOCOL_H}
            DEPENDS ${PROTOCOL_XML}
        )

        target_sources(wallpaperd PRIVATE ${PROTOCOL_C})
        target_include_directories(wallpaperd PRIVATE ${CMAKE_CURRENT_BINARY_DIR})
    endfunction()

    find_program(WAYLAND_SCANNER NAMES wayland-scanner)
    if(WAYLAND_SCANNER)
        message(STATUS "Found wayland-scanner")
        add_protocol(wayland)
        add_protocol(xdg-shell)
        add_protocol(wlr-layer-shell-unstable-v1)
        target_compile_definitions(wallpaperd PRIVATE -DWD_WLROOTS)
    else()
        message(WARNING "wayland-scanner not found, disabling wlroots output support")
    endif()
endif()

if(WD_AUDIO_VISUALIZER)
    pkg_check_modules(PIPEWIRE QUIET libpipewire-0.3)
    pkg_check_modules(SPA QUIET libspa-0.2)
    set(AUDIO_AVAILABLE OFF)
    if(PIPEWIRE_FOUND AND SPA_FOUND)
        message(STATUS "Found PipeWire and libspa, enabling PipeWire support in audio visualizer")
        target_sources(wallpaperd PRIVATE external/cava/input/pipewire.c)
        target_compile_definitions(wallpaperd PRIVATE WD_PIPEWIRE)
        target_include_directories(wallpaperd PRIVATE ${PIPEWIRE_INCLUDE_DIRS} ${SPA_INCLUDE_DIRS})
        target_compile_options(wallpaperd PRIVATE ${PIPEWIRE_CFLAGS_OTHER} ${SPA_CFLAGS_OTHER})
        set(AUDIO_AVAILABLE ON)
    else()
        message(STATUS "PipeWire or libspa not found, disabling PipeWire support in audio visualizer")
    endif()

    pkg_check_modules(PULSE QUIET libpulse-simple)
    if(PULSE_FOUND)
        message(STATUS "Found PulseAudio, enabling its support in audio visualizer")
        target_sources(wallpaperd PRIVATE external/cava/input/pulse.c)
        target_compile_definitions(wallpaperd PRIVATE WD_PULSE)
        target_include_directories(wallpaperd PRIVATE ${PULSE_INCLUDE_DIRS})
        target_compile_options(wallpaperd PRIVATE ${PULSE_CFLAGS_OTHER})
        set(AUDIO_AVAILABLE ON)
    else()
        message(STATUS "PulseAudio not found, disabling its support in audio visualizer")
    endif()

    pkg_check_modules(PORTAUDIO QUIET portaudio-2.0)
    if(PORTAUDIO_FOUND)
        message(STATUS "Found PortAudio, enabling its support in audio visualizer")
        target_sources(wallpaperd PRIVATE external/cava/input/portaudio.c)
        target_compile_definitions(wallpaperd PRIVATE WD_PORTAUDIO)
        target_include_directories(wallpaperd PRIVATE ${PORTAUDIO_INCLUDE_DIRS})
        target_compile_options(wallpaperd PRIVATE ${PORTAUDIO_CFLAGS_OTHER})
        set(AUDIO_AVAILABLE ON)
    else()
        message(STATUS "PortAudio not found, disabling its support in audio visualizer")
    endif()

    if(AUDIO_AVAILABLE)
        target_sources(wallpaperd PRIVATE
            external/cava/cavacore.c
            external/cava/input/common.c
        )
        target_include_directories(wallpaperd PRIVATE external/cava)
        target_compile_definitions(wallpaperd PRIVATE WD_AUDIO_VISUALIZER=1)

        find_package(Threads REQUIRED)
        target_link_libraries(wallpaperd PRIVATE ${WD_AUDIO_VISUALIZER_LIBS} Threads::Threads)

        if(WD_FFTW_SHARED)
            pkg_check_modules(FFTW3 REQUIRED IMPORTED_TARGET fftw3)
            target_link_libraries(wallpaperd PRIVATE PkgConfig::FFTW3)
        else()
            add_subdirectory(external/fftw3 EXCLUDE_FROM_ALL)
            target_include_directories(fftw3 PUBLIC
                $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/external/fftw3/api>
                $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}/external/fftw3>
            )
            target_link_libraries(wallpaperd PRIVATE fftw3)
            target_include_directories(wallpaperd PRIVATE external/fftw3/api ${CMAKE_CURRENT_BINARY_DIR}/external/fftw3)
        endif()
    else()
        message(WARNING "No audio backend found, disabling audio visualizer suppport")
    endif()
else()
    target_compile_definitions(wallpaperd PRIVATE WD_AUDIO_VISUALIZER=0)
endif()

install(TARGETS wallpaperd RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR})
